#include 'totvs.ch'

/*/{Protheus.doc} U_PROCESSA_ENCERRAMENTO_MEDICOES
    Programa para encerramento das medicoes
    @type  Function

    @see https://tdninterno.totvs.com/display/tec/Try...Catch
    @see (Pedido de Vendas) https://centraldeatendimento.totvs.com/hc/pt-br/articles/7326654842775-Cross-Segmento-TOTVS-Backoffice-Linha-Protheus-SIGAFAT-EXECAUTO-MATA410
    @see (Pedido de Compras) https://tdn.totvs.com/pages/releaseview.action?pageId=6089279
    /*/
Function U_PROCESSA_ENCERRAMENTO_MEDICOES

    Local lExec := FWAlertYesNo('Confirma a execucao da rotina?','Processa Encerramento de Medicoes')

    Private lAbortPrint := .F.

    IF .not. lExec
        Return
    ENDIF

    processa({|| PROCESSA_ENCERRAMENTO_MEDICOES()},"Encerrando Medicoes","Aguarde...")

    FWAlertInfo('Processo concluido','Encerramento de Medicoes')

Return

Static Function PROCESSA_ENCERRAMENTO_MEDICOES

    Local nTamanho := 0

    (cAliasTmp)->(dbEval({|| IF(empty(MARK),nil,nTamanho++)}))

    procregua(nTamanho)

    (cAliasTmp)->(dbGoTop())

    While .not. (cAliasTmp)->(eof())

        IF lAbortPrint
            Exit
        EndIF

        IF .not. empty((cAliasTmp)->MARK)
            (cAliasTmp)->(ENCERRA_MEDICAO())
        EndIF    

        (cAliasTmp)->(dbSkip())

    Enddo

Return

Static Function ENCERRA_MEDICAO

    incproc("PROCESSANDO MEDICAO+ITEM: " + (cAliasTmp)->(Z53_NUMMED + '-' + Z53_ITEM))

    DO CASE

        CASE LEFT(Z53_TIPO,1) == "S" //-- Encerramento da medicao sem integracao
            ENCERRA_SEM_INTEGRACAO()

        CASE LEFT(Z53_TIPO,1) == "C" //-- Encerramento da medicao com inclusao de pedido de compras
            ENCERRA_COM_PEDIDO_DE_COMPRAS()     

        CASE LEFT(Z53_TIPO,1) == "V" //-- Encerramento da medicao com inclusao de pedido de vendas
            ENCERRA_COM_PEDIDO_DE_VENDAS()
        
    END CASE

Return

Static Function ENCERRA_SEM_INTEGRACAO 

    IF  Z53->(DBSetOrder(1),DBSeek(xFilial(alias())+(cAliasTmp)->(Z53_NUMERO+Z53_NUMMED+Z53_ITEM)))

        Z53->(recLock(alias(),.F.))
            Z53->Z53_STATUS := 'E' // Encerrado
            Z53->Z53_PEDIDO := 'XXXXXX' // Sem pedido
        Z53->(msunlock())

    EndIF

    (cAliasTmp)->(recLock(alias(),.F.))
        (cAliasTmp)->Z53_STATUS := 'E' // Encerrado
        (cAliasTmp)->Z53_PEDIDO := 'XXXXXX' // Sem pedido
    (cAliasTmp)->(msunlock())

Return

Static Function ENCERRA_COM_PEDIDO_DE_COMPRAS

    Local aCab := {}
    Local aItem := {}
    Local aItens := {}
    Local cNumPed := ''

    Private lMsErroAuto := .F.

    Z53->(DBSetOrder(1),DBSeek(xFilial(alias())+(cAliasTmp)->(Z53_NUMERO+Z53_NUMMED+Z53_ITEM)))
    Z51->(dbSetOrder(1),dbSeek(xFilial(alias())+Z53->Z53_NUMERO))
    Z52->(dbSetOrder(1),dbSeek(xFilial(alias())+Z53->Z53_NUMERO))

    while .not. Z52->(eof()) .and. Z52->(Z52_FILIAL+Z52_NUMERO) == Z53->(Z53_FILIAL+Z53_NUMERO)

        IF Z52->Z52_CODPRD == Z53->Z53_CODPRD
            Exit
        EndIF

        Z52->(dbSkip())

    Enddo 

    cNumPed := getSxeNum('SC7','C7_NUM')

    While SC7->(dbSetOrder(1),dbSeek(xFilial(alias())+cNumPed))
        confirmSX8()
        cNumPed := getSxeNum('SC7','C7_NUM')
    End    

    aadd(aCab,{'C7_NUM'     ,cNumPed        ,})
    aadd(aCab,{'C7_EMISSAO' ,ddatabase      ,})
    aadd(aCab,{'C7_FORNECE' ,Z51->Z51_CLIENT,})
    aadd(aCab,{'C7_LOJA'    ,Z51->Z51_LOJA  ,})
    aadd(aCab,{'C7_COND'    ,'001'          ,})
    aadd(aCab,{'C7_CONTATO' ,'AUTO'         ,})
    aadd(aCab,{'C7_FILENT'  ,cFilAnt        ,})

    aadd(aItem,{'C7_PRODUTO',Z53->Z53_CODPRD,})
    aadd(aItem,{'C7_QUANT'  ,Z53->Z53_QTD   ,})
    aadd(aItem,{'C7_PRECO'  ,Z52->Z52_VLRUNI,})
    aadd(aItem,{'C7_TOTAL'  ,Z53->Z53_VALOR ,})
    aadd(aItem,{'C7_ZNUMMED',Z53->Z53_NUMMED,})
    aadd(aItem,{'C7_ZITEMME',Z53->Z53_ITEM  ,})

    aadd(aItens,aItem)

    msExecAuto({|x,y| mata120(1,x,y,3)},aCab,aItens)

    IF lMsErroAuto
        rollbackSX8()
        mostraErro()
        return .F.
    EndIF

    confirmSX8()

    Z53->(recLock(alias(),.F.))
        Z53->Z53_STATUS := 'E' // Encerrado
        Z53->Z53_PEDIDO := cNumPed // Numero do pedido de compras
    Z53->(msunlock())

    (cAliasTmp)->(recLock(alias(),.F.))
        (cAliasTmp)->Z53_STATUS := 'E' // Encerrado
        (cAliasTmp)->Z53_PEDIDO := cNumPed // Numero do pedido de compras
    (cAliasTmp)->(msunlock())

Return

Static Function ENCERRA_COM_PEDIDO_DE_VENDAS

Return
